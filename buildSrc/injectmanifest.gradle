apply plugin: "kotlin-kapt"

kapt {
  arguments {
    def extension = variant.productFlavors[0].ext.extension
    arg("SOURCE_NAME", extension.name)
    arg("SOURCE_LANG", extension.lang)
    arg("SOURCE_ID", extension.id)
    arg("MANIFEST_HAS_DEEPLINKS", !extension.deepLinks.empty)
  }
}

afterEvaluate {
  android.applicationVariants.all {
    def sources = sourceSets

    outputs.forEach { output ->
      output.processManifestProvider.configure {
        doLast {
          def outputDirectory = multiApkManifestOutputDirectory.get().asFile
          def manifestFile = new File(outputDirectory, "AndroidManifest.xml").absolutePath

          def extension = productFlavors[0].ext.extension
          ManifestGenerator.process(manifestFile, extension, sources)
        }
      }
    }
  }
}

dependencies {
  compileOnly project(Proj.annotation)
  kapt project(Proj.compiler)
}
